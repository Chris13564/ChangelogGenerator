import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	repositories {
		mavenCentral()
	}

	configurations.maybeCreate("pitest")

	dependencies {
		classpath "info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.6"
		pitest "org.pitest:pitest-junit5-plugin:0.11"
	}
}

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/master/build.gradle"
apply plugin: "info.solidsoft.pitest"

group = "com.therandomlabs.changeloggenerator"
version = "2.0.0-pre3"

repositories {
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	implementation "org.slf4j:slf4j-api:2.0.0-alpha1"
	implementation "org.apache.logging.log4j:log4j-core:2.12.1"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.12.1"

	api "com.github.TheRandomLabs:CurseAPI:master-SNAPSHOT"
	api "com.github.TheRandomLabs:CurseAPI-Minecraft:master-SNAPSHOT"

	implementation "com.atlassian.commonmark:commonmark:0.13.1"

	testImplementation "com.github.TheRandomLabs:TRLUtils-IO:master-SNAPSHOT"
}


/* jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = "CLASS"

			limit {
				counter = "INSTRUCTION"
				value = "COVEREDRATIO"
				minimum = 0.8
			}
		}
	}
} */

pitest {
	testPlugin = "junit5"
	threads = 4
	outputFormats = ["XML", "HTML"]
	timestampedReports = false
}

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property "version", version
    filter(ReplaceTokens, tokens: [VERSION: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

jar {
	manifest {
		attributes "Main-Class": "com.therandomlabs.changeloggenerator.Main"
	}

	from {
		configurations.runtimeClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}
